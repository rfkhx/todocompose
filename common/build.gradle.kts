import org.jetbrains.compose.compose

plugins {
    kotlin("multiplatform")
    id("org.jetbrains.compose") version "1.0.0"
    id("com.android.library")
}


val appVersionCode: String by ext
val appVersionName: String by ext

group = "top.ntutn"
version = appVersionName

kotlin {
    android()
    jvm("desktop") {
        compilations.all {
            kotlinOptions.jvmTarget = "11"
        }
    }
    sourceSets {
        val commonMain by getting {
            kotlin.setSrcDirs(listOf("build/generated/source/appInfo", "src/commonMain/kotlin"))
            dependencies {
                api(compose.runtime)
                api(compose.foundation)
                api(compose.material)
            }
        }
        val commonTest by getting
        val androidMain by getting {
            dependencies {
                api("androidx.appcompat:appcompat:1.2.0")
                api("androidx.core:core-ktx:1.3.1")
            }
        }
        val androidTest by getting {
            dependencies {
                implementation("junit:junit:4.13")
            }
        }
        val desktopMain by getting {
            dependencies {
                api(compose.preview)
            }
        }
        val desktopTest by getting
    }
}

android {
    compileSdkVersion(31)
    sourceSets["main"].manifest.srcFile("src/androidMain/AndroidManifest.xml")
    defaultConfig {
        versionCode = appVersionCode.toInt()
        versionName = appVersionName
        minSdkVersion(24)
        targetSdkVersion(31)
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

val generateAppInfoFile =tasks.register("generateAppInfoFile") {
    val content = """
        // Do not modify this file. It's generated by build.gradle.kts in common module
        object AppInfo {
            const val VERSION_CODE = $appVersionCode
            const val VERSION_NAME = "$appVersionName"
        }
    """.trimIndent()
    val appInfoDir = File(project.buildDir,"generated/source/appInfo")
    appInfoDir.mkdirs()
    val appInfoFile = File(appInfoDir, "AppInfo.kt")
    appInfoFile.writeText(content)
}

tasks.matching {
    it.name in listOf("preBuild", "syncDebugLibJars")
}.all {
    dependsOn(generateAppInfoFile)
}